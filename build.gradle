buildscript {
	ext {
		springBootVersion = '1.4.0.RELEASE'
	}
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:1.0.1"
	}
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'

jar {
	baseName = 'ozwillo-dc-exporter'
	version = '0.13.0-dev'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

node {
    version = '7.4.0'
	download = true
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.springframework.boot:spring-boot-starter-jersey')
	compile('org.springframework.boot:spring-boot-starter-mail')
	compile('org.springframework.boot:spring-boot-starter-cache')
	compile('org.springframework.retry:spring-retry')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.session:spring-session')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-websocket')
	compile('joda-time:joda-time:2.9.9')
	compile('io.javaslang:javaslang:2.0.4')
    compile('com.ozwillo:ozwillo-java-spring-integration:1.26.0-dev')
	compile('com.github.ben-manes.caffeine:caffeine:2.3.5')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    // required to parse logback-spring.groovy
    // TODO : quite a fat dependency for a configuration file
    runtime('org.codehaus.groovy:groovy:2.4.7')

	compile 'com.squareup.retrofit2:retrofit:2.2.0'
	compile 'com.squareup.retrofit2:converter-jackson:2.2.0'

}

bootRun {
    // needed for hot reload of thymeleaf templates
    addResources = true

	environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "dev"
}

springBoot {
    // exclude Spring Boot Devtools from the fat jar
    excludeDevtools = true

    executable = true
    embeddedLaunchScriptProperties =
            [initInfoDescription: 'Ozwillo Datacore Exporter',
             initInfoShortDescription: 'Ozwillo Datacore Exporter',
             initInfoProvides: 'ozwillo-dc-exporter']
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}

configure(tasks.processResources) {
	// we don't want source JSX in the jar, we only need their transpiled, bundled version in build/
	exclude 'public/jsx/'
	exclude 'public/css/'
}

task frontBundle(type: YarnTask) {
    args = ['run', 'build']
    doLast {
        copy {
            from "${sourceSets.main.resources.srcDirs[0]}/public/build"
            into "${sourceSets.main.output.resourcesDir}/public/build"
        }
    }
}

jar.dependsOn(frontBundle)
frontBundle.dependsOn(yarn_install)
